
class public AdminAPICredentialWindow

    field private BBjButton btnOK!
    field private BBjButton btnCancel!
    field private BBjInputE inpeUsername!
    field private BBjEditBox ebxPassword!
    field private BBjTopLevelWindow window!
    rem field private BBjCheckBox cbtnSaveCredentials!
    
    field private String username!
    field private String password!
    field private Boolean dialogCancelled!
    field private Boolean saveCredentials!

    method public AdminAPICredentialWindow()
    methodend
    
    method private void init(BBjSysGui sysGui!)
        window_width = 350
        window_height = 125

        declare java.awt.Rectangle screenBounds!
        screenBounds! = sysGui!.getSystemMetrics().getScreenBounds()

        screen_width = screenBounds!.getWidth()
        screen_height = screenBounds!.getHeight()

        displayLocationX = (screen_width/2 - window_width/2)
        displayLocationY = (screen_height/2 - window_height/2)

        #window! = sysGui!.addWindow(sysGui!.getAvailableContext(), displayLocationX, displayLocationY, window_width, window_height, "EM Login Credentials", $00090012$)
        #window!.setCallback(BBjAPI.ON_CLOSE, #this!, "closeWindow")
        #window!.setCallback(BBjWindow.ON_KEYPRESS,#this!,"windowKeyPressEvent")
        
        window_next_id = #window!.getAvailableControlID()
        #window!.addStaticText(window_next_id, 10, 20, 75, 25, "Username: ")
        
        window_next_id = #window!.getAvailableControlID()
        #inpeUsername! = #window!.addInputE(window_next_id, 95, 20-5, 200, 25, "")
        
        window_next_id = #window!.getAvailableControlID()
        #window!.addStaticText(window_next_id, 10, 55, 75, 25, "Password: ")
        
        window_next_id = #window!.getAvailableControlID()
        #ebxPassword! = #window!.addEditBox(window_next_id, 95, 55-5, 200, 25, "" , $0400$)
        
        rem window_next_id = #window!.getAvailableControlID()
        rem #cbtnSaveCredentials! = #window!.addCheckBox(window_next_id, 10, 80, 200, 25, "Save Login Credentials")
        
        window_next_id = #window!.getAvailableControlID()
        #btnOK! = #window!.addButton(window_next_id, window_width-10-85-10-85, window_height-10-25, 85, 25, "OK")
        #btnOK!.setCallback(BBjButton.ON_BUTTON_PUSH, #this!, "okButtonPush")
        
        window_next_id = #window!.getAvailableControlID()
        #btnCancel! = #window!.addButton(window_next_id, window_width-10-85, window_height-10-25, 85, 25, "Cancel")
        #btnCancel!.setCallback(BBjButton.ON_BUTTON_PUSH, #this!, "cancelButtonPush")
               
        #inpeUsername!.focus()
    methodend
    
    method public void cancelButtonPush(BBjButtonPushEvent event!)
        #dialogCancelled! = Boolean.TRUE
        #close(#window!)
    methodend
    
    method public void okButtonPush(BBjButtonPushEvent event!)
        rem #saveCredentials! = #cbtnSaveCredentials!.isSelected()
        #username! = #inpeUsername!.getText()
        #password! = #ebxPassword!.getText()
        
        #close(#window!)
    methodend
    
    method public Boolean dialogCancelled()
        methodret #dialogCancelled!
    methodend
    
REM     method public Boolean saveCredentials()
REM         methodret #saveCredentials!
REM     methodend
    
    method public String getUsername()
        methodret #username!
    methodend
    
    method public String getPassword()
        methodret #password!
    methodend
    
    method private void resetContent()
        #username! = null()
        #password! = null()
        #saveCredentials! = Boolean.FALSE
        
        #inpeUsername!.setText("")
        #ebxPassword!.setText("")
        rem #cbtnSaveCredentials!.setSelected(0)
        #dialogCancelled! = Boolean.FALSE
    methodend
    
    method public void open(BBjSysGui sysGui!)
        if(#window! = null()) then
            #init(sysGui!)
            bbjapi().setCustomEventCallback("CloseCredentialWindowEvent", "endOpen")
        endif
        
        #resetContent()
        
        #window!.setVisible(1)
        
        process_events
        
        endOpen:
    methodend   
    
    method public void windowKeyPressEvent(BBjKeypressEvent e!)
        if (e!.getKeyCode() = 13) then 
            #okButtonPush(Null())
         endif
         
         if (e!.getKeyCode() = 27 ) then 
            #cancelButtonPush(Null())
         endif
         
    methodend
    
    method private void close(BBjTopLevelWindow window!)
        window!.setVisible(0)
        bbjapi().postCustomEvent("CloseCredentialWindowEvent", "")
    methodend
    
    method public void closeWindow(BBjCloseEvent event!)
        #dialogCancelled! = Boolean.TRUE
        #close(cast(BBjTopLevelWindow, event!.getControl()))
    methodend
     
    
classend